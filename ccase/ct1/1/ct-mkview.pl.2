
use strict;
use Scaffolds::System;
use Scaffolds::Command;
use Scaffolds::Util;

use Sys::Hostname;
use File::Basename;
use Win32::FileSecurity;
use Win32::Lanman;
use Win32::Perms;
use Win32::Registry;

our $VERSION = "3.0.0";

our $local_host = lc(hostname);
our $local_stgloc = "c:\\ccstg";
our $local_stgloc_share_name = "ccstg";
our $local_unc_stgloc = "\\\\$local_host\\$local_stgloc_share_name";

our $win_common_group = "$ENV{USERDOMAIN}\\Domain Users";

our $global_host = "rvil-ccview";
our $global_stgloc = "\\\\rvil-ccview\\ccase\\views";

our $mcu_versions = "r:/Mcu_Ngp/Versions_8.X/V8.0/Versions";

our $ccase_unix_region = "radvision";
our $ccase_win_region = "radvision_nt";

my $name;
my $raw_flag = 0;
my $dyn_flag = 1;
my $nop_flag = 0;
my $on_server_flag = 0;
my $global_stg_flag = 0;
my $configspec;
my $mcuver;
my $user = $ENV{USERNAME};
our $verbose = 0;

while (@ARGV)
{
	my $a = $ARGV[0];
	if ($a eq '-name')
	{
		shift;
		$name = shift;
	}
	elsif ($a eq '-raw')
	{
		shift;
		$raw_flag = 1;
	}
	elsif ($a eq '-dyn' || $a eq '-dynamic')
	{
		shift;
		$dyn_flag = 1;
	}
	elsif ($a eq '-snap' || $a eq '-snapshot')
	{
		shift;
		$dyn_flag = 0;
		die "Snapshot views are not supported.\n";
	}
	elsif ($a eq '-global')
	{
		shift;
		$global_stg_flag = 0; # disabled
	}
	elsif ($a eq '-server')
	{
		shift;
		$on_server_flag = 1;
	}
	elsif ($a eq '-spec')
	{
		shift;
		$configspec = shift;
	}
	elsif ($a eq '-mcu')
	{
		shift;
		$mcuver = shift;
	}
	elsif ($a eq '-verbose')
	{
		shift;
		$verbose = 1;
	}
	elsif ($a eq '-n')
	{
		shift;
		$nop_flag = 1;
	}
	else
	{
		last if ! other_options($a);
	}
}

$name = "mcu_$mcuver" if !$name && $mcuver;

die "error: view name not specified.\n" if ! $name;
my $tag = ($raw_flag ? "" : $user . "_") . $name;

my $mcu_cspec;
if ($mcuver)
{
	$mcu_cspec = "${mcu_versions}/$mcuver/configspec.txt"; 
	die "error: invalid mcu version: $mcuver.\n" if ! -f $mcu_cspec;
	
	$configspec = $mcu_cspec if !$configspec;
}

die "error: invalid configspec.\n" if $configspec && ! -f $configspec;

if (! $on_server_flag)
{
	create_local_view();
}
else
{
	create_remote_view();
}

if ($configspec)
{
	my $setcs = command("cleartool setcs -tag $tag $configspec", "cannot set configspec", 0);
	if (! $nop_flag && $setcs->retcode)
	{
		my $rmview = new Scaffolds::System("cleartool rmview -tag $tag");
		die "error: cannot set configspec.\n";
	}
}

print "view $tag created.\n";

#----------------------------------------------------------------------------------------------

sub find_stgloc
{
	# this is kind of tricky, since there's no one-stop config for the storage location
	
	# look in registry (HKCU\Software\Atria\ClearCase\CurrentVersion\clearviewtool)
	my $stg = find_stgloc_in_reg();
	return $stg if keys %$stg;
	
	# look for a local view and deduce from its stgloc
	$stg = find_local_view();
	return $stg if keys %$stg;
	
	# create a stgloc at $local_stgloc. If directory exists, fail.
	$stg = make_stgloc();
	return $stg if keys %$stg;
	return {};
}

#----------------------------------------------------------------------------------------------

sub print_stgloc
{
	my ($stg) = @_;
	return if ! keys %$stg;
	print "local: " . $stg->{local} . "\n";
	print "global: " . $stg->{global} . "\n";
}

#----------------------------------------------------------------------------------------------

sub find_stgloc_in_reg
{
	my ($locstg, $globstg);

	verbose("check registry (HKCU/Software/Atria/ClearCase/CurrentVersion/clearviewtool) for storage location");

	my $key;
	my %values;
	$HKEY_CURRENT_USER->Open("Software\\Atria\\ClearCase\\CurrentVersion\\clearviewtool", $key) or return {};
	$key->GetValues(\%values);
	$globstg = $values{"Last View Dir"}->[2];
	$key->Close();
	return {} if ! $globstg;

	my ($share, $share_path) = get_share_name($globstg);
	my $net_share = new Scaffolds::System("net share $share");
	foreach ($net_share->out)
	{
		$locstg = $1 if /Path *(.*)/;
	}
	return {} if ! $locstg || ! -d $locstg; # path should actually exist
	return { local => $locstg . "\\$share_path", global => $globstg };
}

#----------------------------------------------------------------------------------------------

# from UNC spec (\\host\share\dir) extract (share, dir)

sub get_share_name
{
	my ($d) = @_;
	my @p;
	my $d1;
	while (1)
	{
		$d1 = $d;
		$d = dirname($d);
		last if $d eq $d1;
		push @p, basename($d1);
	}
	@p = reverse(@p);
	my $host = $p[0];
	my $share = $p[1];
	shift @p;
	shift @p;
	return ($share, join("\\", @p));
}

#----------------------------------------------------------------------------------------------

sub find_local_view
{
	my $localhost = lc(hostname);
	my $lsview = new Scaffolds::System("cleartool lsview -short -quick -host $localhost");
	my $view = $lsview->out(0);
	return {} if ! $view;
	my $lsview = new Scaffolds::System("cleartool lsview -long $view");
	my ($locstg, $globstg);
	foreach ($lsview->out)
	{
		$globstg = $1 if /Global path\: (.*)/;
		$locstg = $1 if /View server access path\: (.*)/;
	}
	
	return {} if !$locstg || !$globstg;

	$globstg = dirname($globstg);
	$locstg = dirname($locstg);
	return { local => $locstg, global => $globstg };
}

#----------------------------------------------------------------------------------------------

sub make_stgloc
{
	my $ok = create_dir_with_share($local_stgloc, $local_stgloc_share_name, 'ClearCASE Views Storage');
	return $ok ? { local => $local_stgloc, global => $local_unc_stgloc } : {};
}

#----------------------------------------------------------------------------------------------

sub create_dir_with_share
{
	my ($dir, $share, $desc) = @_;

	if (-d $dir)
	{
		# if dir is empty, remove it and later recreate, to setup the right permissions
		rmdir($dir) or return 0; # will fail if dir is not empty
	}
	mkdir($dir) or return 0;

	add_file_permissions($dir, $win_common_group, [qw(FULL GENERIC_ALL)]) or return 0; # directories get FULL GENERIC_ALL
	return add_share($dir, $share, $desc, $win_common_group);
}

#----------------------------------------------------------------------------------------------

sub add_file_permissions
{
	my ($dir, $group, $perm_list) = @_;
	my @new_perm = @{$perm_list};
	my %perm = ();
	Win32::FileSecurity::Get($dir, \%perm) or return 0;
	$perm{$group} = Win32::FileSecurity::MakeMask(@new_perm) or return 0;
	Win32::FileSecurity::Set($dir, \%perm) or return 0;
	return 1;
}

#----------------------------------------------------------------------------------------------

# returns 0 on failure

sub add_share
{
	my ($dir, $share, $desc, $group) = @_;

	my $perms = new Win32::Perms() or return 0;;
	$perms->Allow($group, FULL_SHARE) or return 0;;

	my $jojo = $perms->GetSD(SD_RELATIVE);
	my $rc = Win32::Lanman::NetShareAdd("\\\\$local_host",
		{	netname => $share,
			type => Win32::Lanman::STYPE_DISKTREE,
			remark => $desc,
			security_descriptor => $perms->GetSD(SD_RELATIVE),
			permissions => 0, # Win32::Lanman::ACCESS_ALL,
			max_uses => -1,
			path => $dir});

	$perms->Close();
	
	return !! $rc;
}

#----------------------------------------------------------------------------------------------

sub create_local_view
{
	my ($lstg, $gstg);
	if (! $global_stg_flag)
	{
		my $stg = find_stgloc();
		die "error: failed to determine storage location.\n" if ! keys %$stg;
		$lstg = $stg->{local};
		$gstg = $stg->{global};
	}
	else
	{
		# this is currently disabled (also: not tested)
		$lstg = $gstg = $global_stgloc;
	}

	die "error: view exists.\n" if -d "$lstg\\$tag.vws";

	my $region = $ccase_win_region;
	my $host = $local_host;

	my $mkview_cmd = "cleartool mkview -tag $tag -tmode unix -region $region -shareable_dos " . 
		"-host $host " . 
		"-hpath $lstg\\$tag.vws -gpath $gstg\\$tag.vws $lstg\\$tag.vws";
	if (! $nop_flag)
	{
		my $mkview = new Scaffolds::System($mkview_cmd);
		die "cannot create view.\n" if $mkview->retcode;
	}
	else
	{
		print "$mkview_cmd\n";
	}
}

#----------------------------------------------------------------------------------------------

sub create_remote_view
{
	my $region = $ccase_unix_region;
	my $host = $global_host;

	my $putty_path = "r:/Mcu_Ngp/Utilities/Putty";
	my $rexec = "$putty_path/plink-batch -agent -l $user $host";

	my $unix_path = "/cm/views/NBU/$tag.vws";
	my $unc_path = "'\\\\'$host'\\cm\\views\\NBU\\'$tag.vws";
	my $unix_ct = "/opt/rational/clearcase/bin/cleartool";
	my $unix_fixprot = "/opt/rational/clearcase/linux_x86/etc/utils/fix_prot";
	my $unix_group = "rvil_ccusers";

	my $mkview_cmd = "$unix_ct mkview " .
		"-tag $tag " .
		"-tmode transparent " .
		"-region $ccase_unix_region " .
		"-host $host " .
		"-hpath $unix_path " .
		"-gpath $unix_path " .
		"$unix_path";
	
	my $r_mkview_cmd = "$rexec \"$mkview_cmd\"";
	if (! $nop_flag)
	{
		my $mkview = new Scaffolds::System($r_mkview_cmd);
		die "error: cannot create view on server\n" if $mkview->retcode;
	}
	else
	{
		print "$r_mkview_cmd\n";
	}

	my $mktag = "$unix_ct mktag -view " .
		"-tag $tag " .
		"-reg $ccase_win_region " .
		"-host $host " .
		"-gpath $unc_path " .
		"$unix_path";
	my $r_mktag_cmd = "$rexec \"$mktag\"";
	if (! $nop_flag)
	{
		my $rc = system("$r_mktag_cmd > mktag.out 2>&1");
		die "error: cannot create view tag on server\n" if $rc;
	}
	else
	{
		print "$r_mktag_cmd\n";
	}

	my $fix_prot = "$unix_fixprot -force -r -chown $user -chgrp $unix_group -chmod 775 $unix_path";
	my $r_fix_prot_cmd = "$rexec \"$fix_prot\"";
	if (! $nop_flag)
	{
		my $rc = system("$r_fix_prot_cmd > mktag.out 2>&1");
		die "error: cannot setup view permissions on server\n" if $rc;
	}
	else
	{
		print "$r_fix_prot_cmd\n";
	}
	
	$fix_prot = "$unix_fixprot -force -root -chown $user -chgrp $unix_group $unix_path";
	$r_fix_prot_cmd = "$rexec \"$fix_prot\"";
	if (! $nop_flag)
	{
		my $rc = system("$r_fix_prot_cmd > mktag.out 2>&1");
		die "error: cannot setup view permissions on server\n" if $rc;
	}
	else
	{
		print "$r_fix_prot_cmd\n";
	}

	my $startview_cmd = "cleartool startview $tag";
	if (! $nop_flag)
	{
		my $setcs = new Scaffolds::System($startview_cmd);
		die "error: cannot start view\n" if $setcs->retcode;
	}
	else
	{
		print "$startview_cmd\n";
	}
}

#----------------------------------------------------------------------------------------------

sub command
{
	my ($cmd, $err, $abort_on_fail) = @_;
	$abort_on_fail = 0 if ! defined $abort_on_fail;
	if (! $nop_flag)
	{
		my $c = new Scaffolds::System($cmd);
		die "error: $err\n" if $c->retcode;
		return $c;
	}
	else
	{
		print "$cmd\n";
		return 1;
	}
}

#----------------------------------------------------------------------------------------------

sub verbose
{
	my ($text) = @_;
	print "$text\n" if $verbose;
}

#----------------------------------------------------------------------------------------------

sub print_help
{
	print <<END;
Create a view.

usage:
ct1 mkview [-name <name>] [-raw] [-spec <file>] [-mcu <version>] 
           [-dynamic | -snapshot] [-server] [-n] [-verbose]
ct1 mkview [-h | -help | -v | -version]

options:
  -name <name>   view name
  -raw           don't add username prefix to view name
  -spec <file>   configspec file
  -mcu <ver>     create view for mcu version <ver> 
                  (take configspec from r:\Mcu_Ngp\Versions_8.X\V8.0\Versions)
  -dynamic       create dynamic view (default)
  -snapshot      create snapshot view
  -server        create view on server (rvil-ccview) [*]
  -verbose       be verbose: explain everything.
  -n             do nothing, just print commands
  -h             print this message
  -v             print version

[*] Creating a view on the server requires one to enter their password.
    It is possible to automate the process and aviod the password prompt.
    For more information, search http://nbuwiki for ct-mkview.
END
}

